{
  "cells": [
    {
      "cell_type": "raw",
      "metadata": {},
      "source": [
        "---\n",
        "title: \"Overview\"\n",
        "bibliography: references.bib\n",
        "format:\n",
        "  html: \n",
        "    df-print: kable\n",
        "    code-fold: show\n",
        "    code-summary: \"Hide code\"\n",
        "    code-overflow: wrap\n",
        "    toc-title: Page Contents\n",
        "    toc: true\n",
        "    toc-depth: 2\n",
        "    toc-location: right\n",
        "    number-sections: false\n",
        "    html-math-method: katex\n",
        "    smooth-scroll: true\n",
        "editor: source\n",
        "editor_options: \n",
        "  chunk_output_type: console\n",
        "---"
      ],
      "id": "026b9b45"
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "```{=html}\n",
        "<style type=\"text/css\">\n",
        "\n",
        "body, td {\n",
        "   font-size: 13pt;\n",
        "}\n",
        "code.r{\n",
        "  font-size: 9pt;\n",
        "}\n",
        "pre {\n",
        "  font-size: 11pt\n",
        "}\n",
        "</style>\n",
        "```\n",
        "\n",
        "\n",
        "::: {.callout-note icon=false}\n",
        "#### Supporting Information (*in review*) \n",
        "This site provides a demonstrative workflow and supporting information for the publication:  \n",
        "**Generalized Vesicular Stomatitis SEIR-Vector Model Incorporating Pathogen Clearance, Observation Bias, and Parameter Uncertainty**  \n",
        "  \n",
        "Authors:  \n",
        "Humphreys, Pelzel-McCluskey, Shults, Velazquez Salinas, Bertram, McGregor, Cohnstaedt, Swanson, Scroggs, Peters, and Rodriguez    \n",
        "       \n",
        "All code and data are available for download:    \n",
        "[GitHub site: https://github.com/geoepi/seir-vector](https://github.com/geoepi/seir-vector)    \n",
        "\n",
        "Additional supporting information and documents available on the Open Science Framework:  \n",
        "[Project OSF site: TBD](https://osf.io/hf8t2/)    \n",
        "  \n",
        ":::\n",
        "\n",
        "  \n",
        "### Navigation  \n",
        "The menu at right can be used to navigate through the workflow.    \n",
        "\n",
        "\n",
        "```{stan}\n",
        "functions {\n",
        "  real[] seirsei(real t, real[] y, real[] theta, \n",
        "             real[] x_r, int[] x_i) {\n",
        "\n",
        "      real N_h = x_i[1];\n",
        "     \n",
        "      real beta_h = theta[1];\n",
        "      real gamma_h = theta[2];\n",
        "      real sigma_h = theta[3];\n",
        "      real sigma_v = theta[4];\n",
        "      real rho_v = theta[5];\n",
        "      real beta_v = theta[6];\n",
        "      real mu_v = theta[7];\n",
        "\t  real upsilon_h = theta[8];\n",
        "\t  real upsilon_v = theta[9];\n",
        "      real i_0 = theta[10];\n",
        "      real e_0 = theta[11];\n",
        "      real ev_0 = theta[12];\n",
        "      real iv_0 = theta[13];\n",
        "      real N_v = theta[14];\n",
        "      \n",
        "      real init[7] = {N_h - i_0 - e_0, e_0, i_0, 0, N_v - iv_0 - ev_0, ev_0, iv_0}; \n",
        "      real Sh = y[1] + init[1];\n",
        "      real Eh = y[2] + init[2];\n",
        "      real Ih = y[3] + init[3];\n",
        "      real Rh = y[4] + init[4];\n",
        "      real Sv = y[5] + init[5];\n",
        "      real Ev = y[6] + init[6];\n",
        "      real Iv = y[7] + init[7];\n",
        "\t  \n",
        "\t  real lambda_h = upsilon_h * beta_h * Iv / N_v;\n",
        "      real lambda_v = upsilon_v * beta_v * Ih / N_h;\n",
        "\t  \n",
        "\t  real dS_dt = -lambda_h * Sh;\n",
        "      real dE_dt =  lambda_h * Sh - sigma_h * Eh;\n",
        "      real dI_dt = sigma_h * Eh - gamma_h * Ih;\n",
        "      real dR_dt =  gamma_h * Ih;\n",
        "\t  \n",
        "\t  real dSv_dt = mu_v*N_v - lambda_v*Sv - mu_v*Sv;\n",
        "      real dEv_dt = lambda_v*Sv - (sigma_v + mu_v)*Ev;\n",
        "      real dIv_dt =  sigma_v * Ev*rho_v - mu_v*Iv;\n",
        "     \n",
        "      return {dS_dt, dE_dt, dI_dt, dR_dt, dSv_dt, dEv_dt, dIv_dt};\n",
        "  }\n",
        "}\n",
        "data {\n",
        "  int<lower=1> n_days;\n",
        "  real t0;\n",
        "  real ts[n_days];\n",
        "  int N_h;\n",
        "  int N_v;\n",
        "  int cases[n_days];\n",
        "}\n",
        "transformed data {\n",
        "  real x_r[0];\n",
        "  int x_i[1] = { N_h };\n",
        "}\n",
        "parameters {\n",
        "  real<lower=0, upper=1> beta_h;\n",
        "  real<lower=0> gamma_h;\n",
        "  real<lower=0> sigma_h;\n",
        "  real<lower=0> sigma_v;\n",
        "  real<lower=0, upper=1> rho_v;\n",
        "  real<lower=0, upper=1> beta_v;\n",
        "  real<lower=0> mu_v;\n",
        "  real<lower=0> upsilon_h;\n",
        "  real<lower=0> upsilon_v;\n",
        "  real<lower=0> phi_inv;\n",
        "  real<lower=0, upper=1> kappa;\n",
        "  real<lower=0> i_0; \n",
        "  real<lower=0> e_0; \n",
        "  real<lower=0> ev_0; \n",
        "  real<lower=0> iv_0; \n",
        "}\n",
        "transformed parameters{\n",
        "  real y[n_days, 7];\n",
        "  real incidence[n_days - 1];\n",
        "  real phi = 1. / phi_inv;\n",
        "  real theta[14] = {beta_h, gamma_h, sigma_h, sigma_v, rho_v, beta_v, mu_v, upsilon_h, upsilon_v, i_0, e_0, ev_0, iv_0, N_v};\n",
        "  y = integrate_ode_rk45(seirsei, rep_array(0.0, 7), t0, ts, theta, x_r, x_i);\n",
        "  for (i in 1:n_days-1){\n",
        "    incidence[i] = -(y[i+1, 2] - y[i, 2] + y[i+1, 1] - y[i, 1])*kappa + 1e-10;\n",
        "  }\n",
        "}\n",
        "model {\n",
        "  beta_h ~ beta(10, 1);\n",
        "  gamma_h ~ lognormal(log(0.085), 0.1);  \n",
        "  sigma_h ~ lognormal(log(0.15), 0.05);\n",
        "  sigma_v ~ lognormal(log(0.3), 0.1);\n",
        "  rho_v ~ beta(2, 5); \n",
        "  beta_v ~ beta(1, 10);\n",
        "  mu_v ~ lognormal(log(0.1), 0.1);\n",
        "  upsilon_h ~ lognormal(log(0.25), 0.1);\n",
        "  upsilon_v ~ lognormal(log(0.15), 0.1);\n",
        "  phi_inv ~ exponential(2);\n",
        "  kappa ~ beta(5, 10);\n",
        "  i_0 ~ lognormal(0, 0.1);\n",
        "  e_0 ~ lognormal(0, 0.1);\n",
        "  ev_0 ~ lognormal(0, 0.1);\n",
        "  iv_0 ~ lognormal(0, 0.1);\n",
        "\n",
        "  cases[1:(n_days-1)] ~ neg_binomial_2(incidence, phi);\n",
        "}\n",
        "generated quantities {\n",
        "  real duration = 1 / gamma_h;\n",
        "  real incub_h = 1 / sigma_h;\n",
        "  real incub_v = 1/ sigma_v;\n",
        "  real log_lik[n_days-1];\n",
        "  real pred_infected[n_days-1];\n",
        "  \n",
        "  pred_infected = neg_binomial_2_rng(incidence, phi);\n",
        "  \n",
        "  for (i in 1:(n_days - 1)) {\n",
        "    log_lik[i] = neg_binomial_2_lpmf(cases[i] | incidence[i], phi);\n",
        "  }\n",
        "}\n",
        "```"
      ],
      "id": "d3ba2b92"
    }
  ],
  "metadata": {
    "kernelspec": {
      "display_name": "Python 3",
      "language": "python",
      "name": "python3"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 5
}